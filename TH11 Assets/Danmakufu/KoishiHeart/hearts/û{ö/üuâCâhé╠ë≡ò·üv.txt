#東方弾幕風
#Title[本能「イドの解放」]
#Text[貴方が作る東方STG〜東方弾幕風 8　728氏のものを本家に近づけてみました。]
#BGM[]
#Image[]
#BackGround[]
#Player[FREE]
#ScriptVersion[2]

script_enemy_main {
	let imgBoss = "script\img\ExRumia.png";
	let p_circle = GetCurrentScriptDirectory ~ "img\powercircle.png";
	let shotdir = GetCurrentScriptDirectory ~ "heart_bullet.txt";
	let shotSE  = GetCurrentScriptDirectory ~ "se\tan00.wav";
	let bellSE  = GetCurrentScriptDirectory ~ "se\kira01_r.wav";
	let conSE   = GetCurrentScriptDirectory ~ "se\eco00_r.wav";

	let c_ang = 0;	//魔法陣回転用変数
	let c_scale;	//魔法陣の表示倍率
	
	@Initialize {
		SetLife(2000);
		SetTimer(99);
		SetScore(10000000);
		
		SetMovePosition02(GetCenterX(),GetClipMinY +120,30);
		
		LoadGraphic(imgBoss);
		LoadGraphic(p_circle);


		LoadSE(shotSE);
		LoadSE(bellSE);
		LoadSE(conSE);
		
		LoadUserShotData(shotdir);
		
		SetDamageRate(50,0);
		
		CutIn(YOUMU,"本能「イドの解放」",0,0,0,0,0);
		
		TMain;
	}
	
	@MainLoop {
	        SetCollisionA(GetX, GetY, 32);
		SetCollisionB(GetX, GetY, 16);
		
		yield;
	}
	
	@DrawLoop {
		circle;

		SetTexture(imgBoss);
		SetGraphicRect(0,0,64,64);
		SetRenderState(ALPHA);
		SetAlpha(255);
		SetGraphicAngle(0,0,0);
		SetGraphicScale(1, 1);
		DrawGraphic(GetX, GetY);
	}
	
	@Finalize {
		DeleteGraphic(imgBoss);
	}
	
	task TMain {
		let angle = 0;
		let angle2 = 0;
		
		yield;
		
		bombregist();
		
		wait(60);

		con(75);
		wait(90);
		
		loop{
			angle2 = 0;
			loop(12){
				heartshot(GetX(),GetY(), 3.5, -0.0165, 0.25, -angle+angle2, 1, 5, 0.5+rand(-1, 1)/15,750);
				heartshot(GetX(),GetY(), 3.5, -0.0165, 0.25, angle+angle2, 2, 5, -0.5+rand(-1, 1)/15,750);
				angle2 += 30;
			}
			angle += 4;
			PlaySE(shotSE);
			DelayPlaySE(bellSE, 5);
			wait(15);
		}
	}
	
	task heartshot(x,y,speed,accel,speed_min,angle,graph,delay,curve,wait){
		let time = 0;
		let obj = Obj_Create(OBJ_SHOT);//弾オブジェクトを作成
		Obj_SetX(obj,x);//x座標設定
		Obj_SetY(obj,y);//y座標設定;
		Obj_SetSpeed(obj,speed);//速度設定
		Obj_SetAngle(obj, angle);//移動角度設定
		ObjShot_SetGraphic(obj, graph);//画像設定
		ObjShot_SetDelay(obj, delay);//遅延時間設定
		
		while( !Obj_BeDeleted(obj) ){//削除されるまでループを実行
			if(speed + accel > speed_min){
				speed += accel;
			}else{
				speed = speed_min;
				curve = curve*(1+accel);
			}
			Obj_SetSpeed(obj, speed);

			angle += curve;
			Obj_SetAngle(obj, angle);

			if(time == wait){
				Obj_SetSpeed(obj, 0.5);
				ObjShot_FadeDelete(obj);
			}
			time += 1;
			
			yield;
		}
	}

	//遅延SE
	task DelayPlaySE(let sePass, let delay){
		loop(delay){yield;}
		PlaySE(sePass);
	}

	function con(let time){
		Concentration01(time);
		PlaySE(conSE);
	}

	function wait(w) {
        	loop(w) { yield; }
	}

	task bombregist(){
		let bregist = 0;
		loop{
			if(OnBomb == true){
				SetInvincibility(3000);
				bregist = 1;
			}
			else if(OnBomb == false && bregist == 1){
				SetInvincibility(0);
				bregist = 0;
			}
			wait(1);
		}
	}

//魔法陣
	sub circle{
		SetTexture(p_circle);
		SetGraphicRect(0,0,266,266);
		SetRenderState(ADD);
		c_ang -= 7.5;	//回転速度
		if(c_ang >= 360 / 0.45){c_ang -= 360 / 0.45;}
		SetGraphicAngle(0,0,c_ang);
		c_scale = (0.06 * sin(c_ang * 0.45)) + 0.9;
		SetGraphicScale(c_scale, c_scale);

		DrawGraphic(GetX, GetY);
	}
}